#!/usr/bin/perl
# a rewrite of the mildew script using Bread::Board
use Bread::Board;
use File::Slurp qw(slurp);
use Getopt::Long;
use lib 'src';



# print the help message
sub help {
print <<'HELP';
Usage: mildew [switches] [--] [programfile] [arguments]
  -B<backend>     execute using the <backend> backend
  -C<backend>     compile using the <backend> backend
                  (valid backends are: via-C)
  -e
  -o <file>       Place the output into <file>
HELP
exit;
}

# get command line options
my ($C,$B,$help,$e,$output);
Getopt::Long::Parser->new( config => [qw( bundling no_ignore_case pass_through require_order)], )->getoptions(
    "C=s" => \$C,
    "B=s" => \$B,
    'h|help' => \$help,
    'e=s' => \$e,
    'o=s' => \$output
) || help;
help if $help;


my $source;
if ($e) {
    $source = $e;
} elsif ($ARGV[0]) {
    $source = slurp($ARGV[0]);
} else {
    $source = join('', <STDIN>);
}

if ($C and $B) {
    die "You can't specify both -C and -B.\n";
} elsif (!$C and !$B) {
    if ($output) {
        $C = 'via-C';
    } else {
        $B = 'via-C';
    }
}

my %backends = (
    "via-C"   => 'Mildew::Backend::C',
    "desugar" => 'Mildew::Backend::Desugar',
);

if ($C and !$backends{$C}) {
    die "Unknown backend $C passed to -C.";
} elsif ($B and !$backends{$B}) {
    die "Unknown backend $B passed to -B.";
}

my $c = container 'Mildew' => as {
    service 'backend'  => (class => $backends{$C // $B});
    service 'parser'  => (class => 'Mildew::Parser');
    service 'compiler' => (class => 'Mildew::Compiler',dependencies=>{backend=>depends_on('backend'),parser=>depends_on('parser')});
};

if ($C) {
    $c->fetch('compiler')->get->compile($source,$output);
} elsif ($B) {
    $c->fetch('compiler')->get->run($source);
}
